#include "..\..\third_party\InnoDependencyInstaller\CodeDependencies.iss"

; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "AnyPortal"
#define MyAppVersion ""
#define MyAppPublisher "AnyPortal"
#define MyAppURL "https://github.com/AnyPortal/AnyPortal"
#define MyAppExeName "anyportal.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A6A3479C-23C5-49A3-AB3F-1E0E5B243487}
AppName={#MyAppName}
;AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
UninstallDisplayIcon={app}\{#MyAppExeName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
; Uncomment the following line to run in non administrative install mode (install for current user only).
; PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=commandline
SolidCompression=yes
WizardStyle=modern
SetupIconFile=..\..\assets\icon\icon.ico

[Languages]
Name: "arabic"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Arabic.isl"
Name: "english"; MessagesFile: "..\..\third_party\issrc\Files\Default.isl"
Name: "french"; MessagesFile: "..\..\third_party\issrc\Files\Languages\French.isl"
Name: "german"; MessagesFile: "..\..\third_party\issrc\Files\Languages\German.isl"
Name: "japanese"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Japanese.isl"
Name: "korean"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Korean.isl"
Name: "portuguese"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Russian.isl"
Name: "spanish"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Spanish.isl"
Name: "turkish"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Turkish.isl"
Name: "chinesesimplified"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\ChineseSimplified.isl"
Name: "chinesetraditional"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\ChineseTraditional.isl"
Name: "hindi"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\Hindi.islu"
Name: "indonesian"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\Indonesian.isl"
Name: "persian"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\Persian.isl"
Name: "thai"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\Thai.isl"
Name: "vietnamese"; MessagesFile: "..\..\third_party\issrc\Files\Languages\Unofficial\Vietnamese.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\..\build\windows\x64\runner\Release\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\build\windows\x64\runner\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[InstallDelete]
Type: "filesandordirs"; Name: "{app}"

[Code]
procedure CreateShutdownFlag();
var
  AppDataPath: string;
begin
  AppDataPath := ExpandConstant('{userappdata}\com.github.anyportal\anyportal');
  if not DirExists(AppDataPath) then
    CreateDir(AppDataPath);

  SaveStringToFile(AppDataPath + '\pending_installer_exit.flag', '', False);
end;

procedure RemoveShutdownFlag();
var
  FlagFile: string;
begin
  FlagFile := ExpandConstant('{userappdata}\com.github.anyportal\anyportal\pending_installer_exit.flag');
  if FileExists(FlagFile) then
    DeleteFile(FlagFile);
end;

function InitializeSetup(): Boolean;
begin
  CreateShutdownFlag();
  Dependency_AddVC2015To2022;
  Result := True;
end;

procedure DeinitializeSetup();
begin
  RemoveShutdownFlag();
end;
